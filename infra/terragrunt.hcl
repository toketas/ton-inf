locals {
  account_vars   = read_terragrunt_config(find_in_parent_folders("account.hcl"))
  region_vars    = read_terragrunt_config(find_in_parent_folders("region.hcl"))
  common_tags    = yamldecode(file("${find_in_parent_folders("common_tags.yaml")}"))
  default_custom = { "Custom Tags" = "False" }
  account_id     = local.account_vars.locals.aws_account_id
  environment    = local.account_vars.locals.environment
  aws_region     = local.region_vars.locals.aws_region
}

#
# Generate AWS provider based on project region and temporary sts credencials generated by Vault in the above code block
#
generate "provider" {
  path      = "provider.tf"
  if_exists = "overwrite_terragrunt"
  contents  = <<EOF
provider "aws" {
  allowed_account_ids = ["${local.account_id}"]
  region              = "${local.aws_region}"

  profile = "seu-profile"
}
EOF
}

#
# Remote State config managed in core-management AWS account.
# To Run this code you need a AWS credential with permissions on this S3 bucket and DynamoDB table(from core-management AWS account).
# This code is ran before terraform and .tf files, so we cannot use a dynamic credencial generated by Vault.
#
remote_state {
  backend = "s3"
  config = {
    encrypt        = true
    bucket         = "ton-terraform-state"
    key            = "${path_relative_to_include()}/terraform.tfstate"
    region         = "us-east-1"
    dynamodb_table = "ton-terraform-lock"
    role_arn       = "arn:aws:iam::xxxxxx:role/TerraformRemoteState"
  }
  generate = {
    path      = "backend.tf"
    if_exists = "overwrite_terragrunt"
  }
}

#
# Terragrunt inputs which are shared among all downstream projects
#
inputs = {
  tags = merge(
    {
      Terraform     = true
      TerraformPath = path_relative_to_include()
    },
    local.common_tags,
    fileexists(find_in_parent_folders("custom_tags.yaml", "inexsistent.file")) ?
        yamldecode(file("${find_in_parent_folders("custom_tags.yaml")}")) : local.default_custom,
  )

}

terraform_version_constraint  = ">= 0.15"
terragrunt_version_constraint = ">= 0.29"
